if (isModule.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'

}
apply plugin: 'com.jakewharton.butterknife'//作为lib时加上这句话

android {
    compileSdkVersion rootProject.ext.compileSdkVersion


    defaultConfig {
        if (isModule.toBoolean()) {
            //组件模式下设置applicationId
            applicationId "com.zyh.libone"
        }

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    /*
   * java插件引入了一个概念叫做SourceSets，通过修改SourceSets中的属性，
   * 可以指定哪些源文件（或文件夹下的源文件）要被编译，
   * 哪些源文件要被排除。
   * */
    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
    // Butterknife requires Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //设置了resourcePrefix值后，所有的资源名必须以指定的字符串做前缀，否则会报错。
    //但是resourcePrefix这个值只能限定xml里面的资源，并不能限定图片资源，所有图片资源仍然需要手动去修改资源名。
//    resourcePrefix "one_"
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    annotationProcessor "com.github.mzule.activityrouter:compiler:$rootProject.activityrouter_annotationProcessor"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknife_annotationProcessor"
    api project(':libcommon')


}
